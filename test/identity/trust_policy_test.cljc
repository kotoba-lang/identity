(ns identity.trust-policy-test
  (:require [clojure.test :refer [deftest is testing]]
            [identity.trust-policy :as policy]))

(def verified
  {:identity.eas/attestation {:recipient "0xA00366234D29d4F882088048c0B2fa0dB7302D4E"}
   :identity/trust-claims
   [{:identity.causal/id "human-passport:10:0xabc:sybil-resistance"
     :identity.causal/valid-until "1790000000"}]})

(deftest only-one-exact-action-has-an-active-human-policy
  (is (some? (policy/human-passport-policy
              "https://itonami.cloud" "identity.sybil-step-up")))
  (is (nil? (policy/human-passport-policy
             "https://murakumo.cloud" "identity.sybil-step-up")))
  (is (nil? (policy/human-passport-policy
             "https://itonami.cloud" "money.transfer"))))

(deftest verified-recipient-must-be-the-principal-account
  (let [p (policy/human-passport-policy
           "https://itonami.cloud" "identity.sybil-step-up")]
    (is (:allowed? (policy/authorize-human-passport
                    p "0xa00366234d29d4f882088048c0b2fa0db7302d4e" verified)))
    (is (false? (:allowed? (policy/authorize-human-passport
                            p "0x0000000000000000000000000000000000000001"
                            verified))))
    (is (= :subject/recipient-mismatch
           (:reason (policy/authorize-human-passport
                     p "0x0000000000000000000000000000000000000001"
                     verified))))))

(deftest a-step-up-never-grants-a-capability
  (let [decision (policy/authorize-human-passport
                  policy/itonami-human-passport-policy
                  "0xa00366234d29d4f882088048c0b2fa0db7302d4e"
                  verified)]
    (is (= :evidence-only (:effect decision)))
    (is (false? (:grants-capability? decision)))))
