(ns identity.adapters.erc8004-test
  (:require [clojure.test :refer [deftest is testing]]
            [identity.adapters.erc8004 :as erc8004]
            [identity.causal :as causal]
            [identity.validate :as validate]))

(defn- address [c] (str "0x" (apply str (repeat 40 c))))
(def coordinate {:namespace "eip155" :chain-id 1
                 :identity-registry (address "1")
                 :reputation-registry (address "2")
                 :validation-registry (address "3")})
(def clients #{(address "a")})
(def validators #{(address "b")})
(def registration
  {:type erc8004/registration-type :name "Kotoba agent"
   :description "A bounded Kotoba agent" :image "ipfs://agent"
   :services [{:name "A2A" :endpoint "https://agent.example/a2a"}]
   :x402-support true :active true :supported-trust ["reputation" "validation"]
   :registrations [{:agent-id 7
                    :agent-registry (str "eip155:1:" (:identity-registry coordinate))}]})
(def chain-agent {:owner (address "c") :agent-uri "ipfs://registration"
            :agent-wallet (address "d") :wallet-verified? true
            :registration registration})
(def policy {:policy-cid "bafy-agent-policy" :issued-at "2026-08-27T00:00:00Z"
             :observed-at 1800000000
             :reputation {:allowed-clients clients :minimum-count 2 :minimum-score 80}
             :validation {:allowed-validators validators :minimum-count 1 :minimum-score 90}})

(defn- reader [agent* reputation validation]
  (erc8004/static-reader
   {:agents {7 agent*}
    :reputations {[7 clients] reputation}
    :validations {[7 validators] validation}}))

(defn- problem [f]
  (try (f) nil (catch #?(:clj Exception :cljs :default) e
                 (:identity.erc8004/problem (ex-data e)))))

(deftest verifies-three-registry-agent-trust-with-explicit-allowlists
  (let [bundle (erc8004/verify! (reader chain-agent {:count 3 :score 91}
                                              {:count 2 :score 95})
                                coordinate 7 policy)]
    (is (= :agent (get-in bundle [:identity/subject :identity.subject/type])))
    (is (validate/valid? (:identity/subject bundle)))
    (is (every? validate/valid? (:identity/evidence bundle)))
    (is (every? validate/valid? (:identity/attestations bundle)))
    (is (every? causal/valid? (:identity/trust-claims bundle)))
    (is (= #{:erc8004/reputation-qualified :erc8004/validation-qualified}
           (set (map :trust.claim/predicate (:identity/trust-claims bundle)))))
    (is (= (:agent-wallet chain-agent) (:identity.erc8004/agent-wallet bundle)))
    (is (empty? (:identity/refusals bundle)))))

(deftest below-threshold-is-a-refusal-not-a-trust-claim
  (let [bundle (erc8004/verify! (reader chain-agent {:count 1 :score 99}
                                              {:count 2 :score 50})
                                coordinate 7 policy)]
    (is (empty? (:identity/trust-claims bundle)))
    (is (= #{:reputation/insufficient-count :validation/below-threshold}
           (set (map :identity.refusal/code (:identity/refusals bundle)))))))

(deftest sybil-sensitive-summaries-require-allowlists
  (is (= :policy/reputation-client-allowlist
         (problem #(erc8004/verify! (reader chain-agent nil nil) coordinate 7
                                    (assoc-in policy [:reputation :allowed-clients] #{})))))
  (is (= :policy/validation-validator-allowlist
         (problem #(erc8004/verify! (reader chain-agent nil nil) coordinate 7
                                    (assoc-in policy [:validation :allowed-validators] #{}))))))

(deftest registration-must-bind-back-to-the-current-registry
  (testing "an inactive document"
    (is (= :registration/inactive
           (problem #(erc8004/verify!
                      (reader (assoc-in chain-agent [:registration :active] false) nil nil)
                      coordinate 7 (dissoc policy :reputation :validation))))))
  (testing "a document for another registry or token"
    (is (= :registration/registry-binding
           (problem #(erc8004/verify!
                      (reader (assoc-in chain-agent [:registration :registrations 0 :agent-id] 8)
                              nil nil)
                      coordinate 7 (dissoc policy :reputation :validation)))))))

(deftest reputation-and-validation-registries-must-point-to-the-identity-registry
  (let [wrong (address "f")
        bad-reader (erc8004/static-reader
                    {:registry-bindings
                     {:reputation-identity-registry wrong
                      :validation-identity-registry (:identity-registry coordinate)}
                     :agents {7 chain-agent}})]
    (is (= :registry/reputation-identity-binding
           (problem #(erc8004/verify! bad-reader coordinate 7
                                      (dissoc policy :reputation :validation)))))))

(deftest reputation-only-deployment-does-not-invent-a-validation-registry
  (let [coordinate (dissoc coordinate :validation-registry)
        policy (dissoc policy :validation)
        bundle (erc8004/verify! (reader chain-agent {:count 3 :score 91} nil)
                                coordinate 7 policy)]
    (is (= [:erc8004/reputation-qualified]
           (mapv :trust.claim/predicate (:identity/trust-claims bundle))))
    (is (= 2 (count (:identity/evidence bundle))))))
