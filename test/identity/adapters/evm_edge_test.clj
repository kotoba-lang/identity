(ns identity.adapters.evm-edge-test
  (:require [clojure.test :refer [deftest is testing]]
            [identity.adapters.evm-edge :as edge])
  (:import (java.nio.charset StandardCharsets)))

(defn- word [n] (format "%064x" (biginteger n)))
(defn- bytes32 [ch] (apply str (repeat 64 ch)))
(defn- address-word [ch]
  (str (apply str (repeat 24 "0")) (apply str (repeat 40 ch))))
(defn- hex-text [s]
  (apply str (map #(format "%02x" (bit-and 0xff %))
                  (.getBytes s StandardCharsets/UTF_8))))
(defn- dynamic-bytes [raw]
  (str (word (quot (count raw) 2)) raw
       (apply str (repeat (mod (- 64 (mod (count raw) 64)) 64) "0"))))

(def uid (str "0x" (bytes32 "1")))
(def schema-uid (str "0x" (bytes32 "2")))
(def attestation-result
  (let [tuple (str (bytes32 "1") (bytes32 "2")
                   (word 100) (word 200) (word 0) (bytes32 "0")
                   (address-word "3") (address-word "a")
                   (word 1) (word 320) (dynamic-bytes "abcd"))]
    (str "0x" (word 32) tuple)))
(def schema-result
  (let [schema "bool verified"]
    (str "0x" (word 32) (bytes32 "2") (address-word "b")
         (word 1) (word 128) (dynamic-bytes (hex-text schema)))))

(deftest decodes-edge-eas-results
  (let [record (edge/decode-eas-attestation-result attestation-result)
        schema (edge/decode-eas-schema-result schema-result)]
    (is (= uid (:uid record)))
    (is (= schema-uid (:schema-uid record)))
    (is (= "0x3333333333333333333333333333333333333333" (:recipient record)))
    (is (= "0xaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa" (:attester record)))
    (is (= "0xabcd" (:data record)))
    (is (= "bool verified" (:schema schema)))))

(deftest malformed-edge-eas-results-fail-closed
  (testing "dynamic offset cannot escape the response"
    (is (= :abi/out-of-bounds
           (try
             (edge/decode-eas-schema-result (str "0x" (word 4096)))
             nil
             (catch clojure.lang.ExceptionInfo e
               (:identity.evm-edge/problem (ex-data e))))))))

(deftest decodes-edge-erc8004-results
  (is (= "0xaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa"
         (edge/decode-address-result (str "0x" (address-word "a")) "owner")))
  (is (= "ipfs://agent"
         (edge/decode-string-result
          (str "0x" (word 32) (dynamic-bytes (hex-text "ipfs://agent"))) "uri")))
  (is (= {:count 3 :score 175/2}
         (edge/decode-reputation-summary-result
          (str "0x" (word 3) (word 875) (word 1))))))
